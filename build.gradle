/**
 * Copyright (c) 2017 Dell Inc., or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.github.jengelman.gradle.plugins', name:'shadow', version: shadowGradlePlugin
    }
}

group = "io.pravega"
version = connectorVersion

apply plugin: 'eclipse'
apply from: 'gradle/idea.gradle'
apply from: 'gradle/java.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/findbugs.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/maven.gradle'

repositories {
    mavenLocal()
    if (findProperty("repositoryUrl")) {
        maven {
            url findProperty("repositoryUrl")
        }
    }
    else {
        jcenter()
        mavenCentral()
        maven {
            url "https://repository.apache.org/snapshots"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 60, 'minutes'
        cacheChangingModulesFor 60, 'minutes'
    }
}

configurations {
    testCompile.extendsFrom(compileOnly)
    testCompile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    testCompile.exclude group: 'log4j', module: 'log4j'
}

test {
    testLogging {
        onOutput { descriptor, event ->
            if (project.hasProperty('logOutput')) {
                events = ["passed", "failed", "skipped"]
                showStandardStreams = true
                logger.lifecycle(event.message)
            }
        }
    }
}

dependencies {
    compile group: 'io.pravega', name: 'pravega-client', version: pravegaVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jApiVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsVersion

    compileOnly group: 'org.apache.flink', name: 'flink-streaming-scala_2.11', version: flinkVersion // provided by application
    compileOnly group: 'org.apache.flink', name: 'flink-table_2.11', version: flinkVersion // provided by application
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion // provided by flink-runtime

    testCompile group: 'junit', name: 'junit', version: junitVersion
    testCompile group: 'org.mockito', name: 'mockito-all', version: mockitoVersion
    testCompile group: 'org.apache.flink', name: 'flink-tests_2.11', version: flinkVersion
    testCompile group: 'org.apache.flink', name: 'flink-test-utils_2.11', version: flinkVersion
    testCompile group: 'org.apache.flink', name: 'flink-streaming-contrib_2.11', version: flinkVersion
    testCompile group: 'org.apache.flink', name: 'flink-streaming-java_2.11', classifier: 'tests', version: flinkVersion
    testCompile group: 'org.apache.flink', name: 'flink-runtime_2.11', classifier: 'tests', version: flinkVersion
    testCompile group: 'io.pravega', name: 'pravega-standalone', version: pravegaVersion

    //  configuring the shaded pom dependencies
    shadow group: 'org.slf4j', name: 'slf4j-api', version: slf4jApiVersion
    shadow group: 'org.apache.flink', name: 'flink-streaming-java_2.11', version: flinkVersion
}

shadowJar {

    relocate "Class50.lombok", "io.pravega.shaded.Class50"
    relocate "com.squareup", "io.pravega.shaded.com.squareup"
    relocate "com.zwitserloot", "io.pravega.shaded.com.zwitserloot"
    relocate "edu.umd", "io.pravega.shaded.edu.umb"
    relocate "io.opencensus", "io.pravega.shaded.io.opencensus"
    relocate "info.ganglia", "io.pravega.shaded.info.ganglia"
    relocate "javax.annotation", "io.pravega.shaded.javax.annotation"
    relocate "lombok", "io.pravega.shaded.lombok"
    relocate "net.jcip", "io.pravega.shaded.net.jcip"
    relocate "okio", "io.pravega.shaded.okio"
    relocate "org.acplt", "io.pravega.shaded.org.acplt"
    relocate "org.objectweb", "io.pravega.shaded.org.objectweb"

    // Shading the libraries which could cause potential version conflicts with flink.
    relocate "com.google", "io.pravega.shaded.com.google"
    relocate "org.apache.commons", "io.pravega.shaded.org.apache.commons"
    relocate "io.grpc", "io.pravega.shaded.io.grpc"
    relocate "io.netty", "io.pravega.shaded.io.netty"

    classifier = null
    mergeServiceFiles()
}

javadoc {
    title = "Pravega Flink Connector"
    failOnError = false
    exclude "**/impl/**";
}
