apply plugin: 'maven'
apply plugin: 'signing'

if (project.hasProperty("doSigning")) {
    signing {
        sign shadowJar
        sign sourceJar
        sign javadocJar
    }
    artifacts {
        shadow signShadowJar.singleSignature
        shadow signSourceJar.singleSignature
        shadow signJavadocJar.singleSignature
    }

    // Default the secretKeyRingFile to the current user's home
    if (!project.property("signing.secretKeyRingFile")) {
        def secretKeyPath = project.file("${System.getProperty("user.home")}/.gnupg/secring.gpg").absolutePath
        project.setProperty("signing.secretKeyRingFile", secretKeyPath)
    }
}

uploadArchives.enabled = false
uploadShadow {
    repositories {
        mavenDeployer {
            // Only configure publishing if a URL was provided
            if (project.hasProperty("publishUrl")) {
                if (publishUrl == "mavenCentral") {
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: publishUsername, password: publishPassword)
                    }
                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: publishUsername, password: publishPassword)
                    }
                }
                else if (publishUrl == "jcenterSnapshot") {
                    repository(url: "https://oss.jfrog.org/artifactory/oss-snapshot-local/") {
                        def bintrayUser = System.getProperty('BINTRAY_USER')
                        def bintrayKey = System.getProperty('BINTRAY_KEY')
                        authentication(userName: bintrayUser, password: bintrayKey)
                    }
                }
                else {
                    repository(url: publishUrl) {
                        // Only configure credentials if they are provided (allows publishing to the filesystem)
                        if (project.hasProperty("publishUsername") && project.hasProperty("publishPassword")) {
                            authentication(userName: publishUsername, password: publishPassword)
                        }
                    }
                }
            }
        }
    }
}

task publishToRepo(dependsOn: uploadShadow) {
    description = "Publish all artifacts to repository"
}

tasks.withType(Upload) {
    repositories.withType(MavenResolver) {
        pom.project {
            name "Pravega Flink Connectors"
            url "http://pravega.io"
            description "Streaming Storage Platform"
            scm {
                url 'https://github.com/pravega/flink-connectors/tree/master'
                connection 'scm:git:git://github.com/pravega/flink-connectors.git'
                developerConnection 'scm:git:https://github.com/pravega/flink-connectors.git'
            }
            licenses {
                license {
                    name 'The Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'fpj'
                    name 'Flavio Junqueira'
                }
            }
        }

        pom.scopeMappings.mappings.remove(configurations.testCompile)

        pom.withXml { xml ->
            // add the 'provided' dependencies based on the 'shadowOnly' configuration
            def dependenciesNode = xml.asNode().appendNode('dependencies')
            project.configurations.shadowOnly.allDependencies.each { dep ->
                if (!(dep instanceof SelfResolvingDependency)) {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', 'provided')
                }
            }
        }

        if (project.hasProperty('doSigning')) {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        }
    }
}

// Configure install task to use the shadow jar/pom
install.configuration = configurations.shadow
MavenPom pom = install.repositories.mavenInstaller.pom
pom.scopeMappings.mappings.remove(configurations.compile)
pom.scopeMappings.mappings.remove(configurations.runtime)
pom.scopeMappings.addMapping(MavenPlugin.RUNTIME_PRIORITY, configurations.shadow, Conf2ScopeMappingContainer.RUNTIME)
